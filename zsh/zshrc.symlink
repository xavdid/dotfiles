#!/usr/bin/env zsh

# set any set local values
if [[ -f ~/.local_pre_setup.zsh ]];then
  source ~/.local_pre_setup.zsh
fi

# export ZSH=~/.oh-my-zsh
export DOTFILES=~/.dotfiles
# all of our zsh files
typeset -U config_files
config_files=($DOTFILES/**/*.zsh)

# Set name of the theme to load.
# Look in ~/.oh-my-zsh/themes/
# export ZSH_THEME=""
# source $ZSH/oh-my-zsh.sh

#  set up autojump
[[ -s "$(brew --prefix)"/etc/autojump.sh ]] && . "$(brew --prefix)"/etc/autojump.sh

# add custom completion scripts
fpath=(~/.zsh/completion "$(brew --prefix)/share/zsh/site-functions" $fpath)

# initialize autocomplete after everything has modified the `fpath`
# none of my scripts modify the fpath afaik, so it's ok to do this before everything else is loaded
# it does mean I can `compdef` in zsh files (useful for adding completions to aliases)
autoload -Uz compinit
compinit

# load the path files
for file in ${(M)config_files:#*/path.zsh}
do
  source $file
done

# load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}
do
  source $file
done


# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}
do
  source $file
done

# initialize direnv
eval "$(direnv hook zsh)"

# bun completions
[ -s "/Users/david/.bun/_bun" ] && source "/Users/david/.bun/_bun"

# initialize prompt: starship w/ custom settings paths
export STARSHIP_CONFIG=$DOTFILES/config/starship.toml
eval "$(starship init zsh)"

# clear screen for test runner
export UTR_CLEAR_PRE_RUN=1

# do any local post-setup hooks
if [[ -f ~/.local_post_setup.zsh ]];then
  source ~/.local_post_setup.zsh
fi

[ -f ~/.fzf.zsh ] && source ~/.fzf.zsh

eval "$(mise activate zsh)"
